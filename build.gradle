buildscript {
    ext {
        nextVersion = "patch"
    }
}

plugins {
    id 'java'
    id 'jacoco'
    id 'com.cinnober.gradle.semver-git' version '2.3.1'
    id 'org.sonarqube' version '2.6.2'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

group 'me.raatiniemi'

// noinspection GroovyUnusedAssignment
sourceCompatibility = JavaVersion.VERSION_1_8

// noinspection GroovyUnusedAssignment
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

// noinspection GroovyAssignabilityCheck
jar {
    manifest {
        attributes(
                'Implementation-Title': 'jcmdr',
                'Implementation-Version': version
        )
    }
}

sonarqube {
    properties {
        properties['sonar.java.binaries'] = 'build/classes/main'
        properties['sonar.java.test.binaries'] = 'build/classes/test'

        properties['sonar.junit.reportPaths'] = 'build/test-results'
        properties['sonar.jacoco.reportPath'] = "${buildDir}/jacoco/test.exec"
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
}

apply from: 'jacoco.gradle'
